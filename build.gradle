group = 'org.athenian'
version = '1.0.0-SNAPSHOT'
//archivesBaseName = 'ros-grpc-bridge'

//sourceCompatibility = 1.8
//targetCompatibility = 1.8

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'io.grpc:grpc-all:1.9.0'
    compile 'com.beust:jcommander:1.72'
    compile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile 'com.typesafe:config:1.3.2'
    compile 'org.assertj:assertj-core:3.9.0'
}

protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
        }
    }

    generateProtoTasks.generatedFilesBaseDir = "${projectDir}/src/generated"

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}


sourceSets {
    main {
        java {
            srcDirs 'src/generated/main/grpc'
            srcDirs 'src/generated/main/java'
        }
    }
}

jar {
    manifest {
        attributes "Main-Class": "org.athenian.RosBridge"
    }

    archiveName 'ros-grpc-bridge.jar'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

clean {
    delete "${projectDir}/src/generated"
}

// Let intellij projects refer to generated code
//idea {
//    module {
//        sourceDirs += file('${projectDir}/src/generated/main/java');
//        sourceDirs += file('${projectDir}/src/generated/main/grpc');
//    }
//}
